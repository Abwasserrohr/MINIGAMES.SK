#
# ==============
# init.sk
# ==============
# init.sk is part of the MINIGAMES.SK library.
# ==============
# > GAME: ColorRun
# ==============

on load:
  wait 1 tick
  mgColorRunHandler("start")

every 1 second:
  feed all players
  heal all players

on place:
  cancel event

on quit:
  if mgGetTemporaryGameData("playerstatus|%player%") is true:
    set {_playersingame} to mgGetTemporaryGameData("playersingame")
    remove 1 from {_playersingame}
    mgSetTemporaryGameData("playersingame",{_playersingame})
    mgSetTemporaryGameData("playerstatus|%player%",false)
    mgCheckColorRunRoundEnd()

on join:
  clear inventory of player
  set gamemode of player to spectator
  set {_loc} to mgGetTemporaryGameData("respawnloc")
  teleport player to {_loc}

on damage:
  if victim is a player:
    cancel event
    if attacker is a player:
      stop
    mgColorRunHandleGameDeath(victim)

on death:
  if victim is a player:
    mgColorRunHandleGameDeath(victim)

on break:
  clear drops
  if mgGetTemporaryGameData("status") is not "ingame":
    cancel event

every 1 tick:
  if mgGetTemporaryGameData("status") is "ingame":
    loop all players:
      set {_loc} to location of loop-player
      remove 1 from y-coord of {_loc}
      loop blocks in radius 1.2 around {_loc}:
        if loop-block is lime concrete:
          set {_bloc} to location of loop-block
          mgColorRunFloor({_bloc})
