#
# ==============
# init.sk
# ==============
# init.sk is part of the MINIGAMES.SK library.
# ==============
# > GAME: Spleef
# ==============

on load:  
  wait 1 tick
  mgSpleefHandler("start")

every 1 second:
  feed all players
  heal all players

on place:
  cancel event
    
on quit:
  if {spleef::playerstatus::%uuid of player%} is set:
    remove 1 from {MG::playersingame}
    delete {spleef::playerstatus::%uuid of player%}
    mgSpleefCheckSpleefRoundEnd()

on join:
  clear inventory of player
  set gamemode of player to spectator
  set {_loc} to mgGetTemporaryGameData("respawnloc")
  teleport player to {_loc}

on damage:
  if victim is a player:
    cancel event
    if attacker is a player:
      send "Kein PvP." to attacker
      stop
    if mgGetTemporaryGameData("status") is "ingame":
      execute console command "/gamemode spectator %victim%"
      clear inventory of victim
      execute console command "/effect %victim% minecraft:resistance 3 5 true"
      remove 1 from {MG::playersingame}
      mgAddCurrentGamePoints("Spleef",victim,"points",1)
      mgAddPlayerToplistScore("Spleef",victim,1)
      set {_loc} to mgGetTemporaryGameData("respawnloc")
      teleport victim to {_loc}
      loop all players:
        if loop-player's gamemode is survival:
          mgAddCurrentGamePoints("Spleef",loop-player,"points",2)
          mgAddPlayerToplistScore("Spleef",loop-player,2)
          add 1 to {_players}
      mgSetSidebarToplist("Spleef","points",5,1)

      mgSpleefCheckSpleefRoundEnd()
    else:
      set {_loc} to mgGetTemporaryGameData("respawnloc")
      teleport victim to {_loc}

on death:
  if victim is a player:
    if mgGetTemporaryGameData("status") is "ingame":
      wait 1 tick
      force victim to respawn
      execute console command "/gamemode spectator %victim%"
      clear inventory of victim
      execute console command "/effect %victim% minecraft:resistance 3 5 true"
      remove 1 from {MG::playersingame}
      mgAddCurrentGamePoints("Spleef",victim,"points",1)
      mgAddPlayerToplistScore("Spleef",victim,1)
      mgSetSidebarToplist("Spleef","points",5,1)
      set {_loc} to mgGetTemporaryGameData("respawnloc")
      teleport victim to {_loc}
      mgSpleefCheckSpleefRoundEnd()

on break:
  clear drops
  if mgGetTemporaryGameData("status") is not "ingame":
    cancel event

on leaves decay:
  cancel event
